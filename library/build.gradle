plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id "de.undercouch.download"
}

android {
    namespace 'com.tyganeutronics.timezonepicker'
    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34
        
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'

    api project(path: ':base-library')
    implementation 'androidx.core:core-ktx:1.13.1'
}

tasks.register('downloadTimeZoneData') {

    // Download timezone data files
    download.run {
        src([
                'https://raw.githubusercontent.com/eggert/tz/main/backward',
                'https://raw.githubusercontent.com/eggert/tz/main/zone.tab'
        ])
        dest new File(rootProject.rootDir, '/library/src/main/assets')
        onlyIfModified true
        useETag 'all'
    }

}

preBuild.dependsOn downloadTimeZoneData