// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = '2.0.20'
    ext.library_version = '1.0.5'

    repositories {
        mavenLocal()
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.5.2'
        classpath 'com.google.gms:google-services:4.4.2'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:3.0.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "de.undercouch:gradle-download-task:5.3.0"
    }
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        google()
    }

    version = "$library_version"
    group = 'com.github.richard-muvirimi'
}

subprojects {
    // Deploying multiple modules
    // https://stackoverflow.com/a/48024281/5956589
    apply plugin: 'maven-publish'

    ext.getArtifactId = { name ->
        if (name.contains('base-library')) {
            return 'com.android.timezonepicker'
        } else {
            return 'timezonepicker'
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId getArtifactId(project.name)
                version project.version

                afterEvaluate {
                    from components.findByName('release')
                }
            }
        }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}
